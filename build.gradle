apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.byteatebit'
version = '0.1.0'

description = """byteatebit-common"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url privateRepositoryUrl }
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.2'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.53-beta'
}

configurations {
    priv
}

task srcJar(type: Jar) {
    classifier = "src"
    from sourceSets.main.allSource
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

artifacts {
    priv jar
    priv srcJar
    archives jar
    archives srcJar
}

def projectPom = {
    name project.name
    packaging jar
    description "byteatebit library with a configuration model, object streams, and utilities"
    url "https://github.com/byteatebit/byteatebit-common"

    scm {
        url "https://github.com/byteatebit/byteatebit-common"
    }

    licenses {
        license {
            name "Apache License, Version 2.0"
            url "https://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

uploadPriv {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: privateUploadReleaseUrl) {
                authentication(userName: byteatebitPrivateUser, password: byteatebitPrivatePassword)
            }
            snapshotRepository(url: privateUploadSnapshotUrl) {
                authentication(userName: byteatebitPrivateUser, password: byteatebitPrivatePassword)
            }
            pom.project projectPom
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: publicUploadReleaseUrl) {
                authentication(userName: byteatebitPublicUser, password: byteatebitPublicPassword)
            }
            snapshotRepository(url: publicUploadSnapshotUrl) {
                authentication(userName: byteatebitPublicUser, password: byteatebitPublicPassword)
            }
            pom.project projectPom
        }
    }
}
